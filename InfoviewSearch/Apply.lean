module

public import InfoviewSearch.Util

public meta section

namespace InfoviewSearch.Apply
open Lean Meta Widget Server ProofWidgets Jsx

structure ApplyLemma where
  name : Premise

/-! ### Checking apply lemmas -/

structure ApplicationInfo where
  numGoals : Nat
  nameLenght : Nat
  replacementSize : Nat
  name : String
  newGoals : Array AbstractMVarsResult
deriving Inhabited

/-- A apply lemma that has been applied to an expression. -/
structure Application extends ApplyLemma where
  /-- The proof of the application -/
  proof : Expr
  /-- The extra goals created by the application -/
  newGoals : Array (MVarId √ó BinderInfo)
  /-- Whether any of the new goals contain another a new metavariable -/
  makesNewMVars : Bool
  info : ApplicationInfo

set_option linter.style.emptyLine false in
/-- If `thm` can be used to apply to `e`, return the applications. -/
def checkApplication (lem : ApplyLemma) (target : Expr) : MetaM (Option Application) := do
  let thm ‚Üê match lem.name with
    | .const name => mkConstWithFreshMVarLevels name
    | .fvar fvarId => pure (.fvar fvarId)
  withTraceNodeBefore `infoview_search (return m!"applying {thm} to {target}") do
  let (mvars, binderInfos, e) ‚Üê forallMetaTelescopeReducing (‚Üê inferType thm)
  let unifies ‚Üê withTraceNodeBefore `infoview_search (return m! "unifying {e} =?= {target}")
    (withReducible (isDefEq e target))
  unless unifies do return none
  try synthAppInstances `infoview_search default mvars binderInfos false false
  catch _ => return none
  let mut newGoals := #[]
  for mvar in mvars, bi in binderInfos do
    unless ‚Üê mvar.mvarId!.isAssigned do
      newGoals := newGoals.push (mvar.mvarId!, bi)

  let makesNewMVars ‚Üê newGoals.anyM fun goal => do
    let type ‚Üê instantiateMVars <| ‚Üê goal.1.getType
    return (type.findMVar? fun mvarId => mvars.any (¬∑.mvarId! == mvarId)).isSome
  let proof ‚Üê instantiateMVars (mkAppN thm mvars)
  let info := {
    numGoals := newGoals.size
    nameLenght := lem.name.length
    replacementSize := ‚Üê newGoals.foldlM (init := 0) fun s g =>
      return (‚Üê ppExpr (‚Üê g.1.getType)).pretty.length + s
    name := lem.name.toString
    newGoals := ‚Üê newGoals.mapM fun g => do abstractMVars (‚Üê g.1.getType)
  }
  return some { lem with proof, newGoals, makesNewMVars, info }

def ApplicationInfo.lt (a b : ApplicationInfo) : Bool :=
  Ordering.isLT <|
  (compare a.1 b.1).then <|
  (compare a.2 b.2).then <|
  (compare a.3 b.3).then <|
  (compare a.4 b.4)

def ApplicationInfo.isDuplicate (a b : ApplicationInfo) : MetaM Bool :=
  pure (a.newGoals.size == b.newGoals.size) <&&>
  a.newGoals.size.allM fun i _ =>
    pure (a.newGoals[i]!.mvars.size == b.newGoals[i]!.mvars.size)
      <&&> isExplicitEq a.newGoals[i]!.expr b.newGoals[i]!.expr

/-- Return the `apply` tactic that performs the application. -/
def tacticSyntax (app : Application) : MetaM (TSyntax `tactic) := do
  let proof ‚Üê withOptions (pp.mvars.set ¬∑ false) (PrettyPrinter.delab app.proof)
  if app.newGoals.isEmpty then
    `(tactic| exact $proof)
  else
    `(tactic| refine $proof)

/-- `ApplyResult` stores the information from an apply lemma that was successful. -/
structure ApplyResult where
  /-- `filtered` will be shown in the filtered view. -/
  filtered : Option Html
  /-- `unfiltered` will be shown in the unfiltered view. -/
  unfiltered : Html
  /-- `info` is used for sorting and comparing apply theorems. -/
  info : ApplicationInfo
  /-- The `pattern` of the first lemma in a section is shown in the header of that section. -/
  pattern : CodeWithInfos
deriving Inhabited

instance : LT ApplyResult where
  lt a b := a.info.lt b.info

instance : DecidableLT ApplyResult := fun a b => by
  dsimp [LT.lt]; infer_instance

set_option linter.style.emptyLine false in
/-- Construct the `Result` from an `Application`. -/
def Application.toResult (app : Application) (pasteInfo : PasteInfo) :
    MetaM ApplyResult := do
  let tactic ‚Üê tacticSyntax app
  let mut newGoals := #[]
  for (mvarId, bi) in app.newGoals do
    -- TODO: think more carefully about which goals should be displayed
    -- Are there lemmas where a hypothesis is marked as implicit,
    -- which we would still want to show as a new goal?
    if bi.isExplicit then
      newGoals := newGoals.push (‚Üê ppExprTagged (‚Üê mvarId.getType))
  let prettyLemma ‚Üê ppPremiseTagged app.name

  let html (showNames : Bool) :=
    mkSuggestionElement tactic pasteInfo (isText := newGoals.isEmpty) <|
      let newGoals := newGoals.map
        (<div> <strong className="goal-vdash">‚ä¢ </strong> <InteractiveCode fmt={¬∑}/> </div>)
      let newGoals := if newGoals.isEmpty then #[.text "Goal accomplished! üéâ"] else newGoals
      .element "div" #[] <| newGoals ++
        if showNames then #[<div> <InteractiveCode fmt={prettyLemma}/> </div>] else #[]
  let lemmaType ‚Üê match app.name with
    | .const name => (¬∑.type) <$> getConstInfo name
    | .fvar fvarId => inferType (.fvar fvarId)
  return {
    filtered := ‚Üê if !app.makesNewMVars then (some <$> html false) else pure none
    unfiltered := ‚Üê html true
    info := app.info
    pattern := ‚Üê forallTelescope lemmaType fun _ e => ppExprTagged e
  }

/-- `generateSuggestion` is called in parallel for all apply lemmas.
- If the lemma succeeds, return a `ApplyResult`.
- If the lemma fails, return `none`
- If the attempt throws an error, return the error as `Html`.

Note: we use two `try`-`catch` clauses, because we rely on `ppConstTagged`
in the first `catch` branch, which could (in principle) throw an error again.
-/
def generateSuggestion (expr : Expr) (pasteInfo : PasteInfo) (lem : ApplyLemma) :
    MetaM <| Task (Except Html <| Option ApplyResult) := do
  BaseIO.asTask <| EIO.catchExceptions (‚Üê dropM do withCurrHeartbeats do
    have : MonadExceptOf _ MetaM := MonadAlwaysExcept.except
    try .ok <$> withNewMCtxDepth do
      Core.checkSystem "infoview_search"
      let some app ‚Üê checkApplication lem expr | return none
      some <$> app.toResult pasteInfo
    catch e => withCurrHeartbeats do
      return .error
        <li>
          An error occurred when processing apply lemma
          <InteractiveCode fmt={‚Üê ppPremiseTagged lem.name}/>:
          <br/>
          <InteractiveMessage msg={‚Üê WithRpcRef.mk e.toMessageData} />
        </li>)
    fun e =>
      return .error
        <li>
          An error occurred when pretty printing apply lemma {.text lem.1.toString}:
          <br/>
          <InteractiveMessage msg={‚Üê WithRpcRef.mk e.toMessageData} />
        </li>



/-- `SectionState` is the part of `WidgetState` corresponding to each section of suggestions. -/
structure SectionState where
  /-- Whether the applications are using a local hypothesis,
  a local theorem, or an imported theorem. -/
  kind : PremiseKind
  /-- The results of the theorems that successfully apply. -/
  results : Array ApplyResult
  /-- The computations for apply theorems that have not finished evaluating. -/
  pending : Array (Task (Except Html <| Option ApplyResult))

def SectionState.update (s : SectionState) : MetaM (Array Html √ó SectionState) := do
  let mut pending := #[]
  let mut results := s.results
  let mut exceptions := #[]
  for t in s.pending do
    if !(‚Üê IO.hasFinished t) then
      pending := pending.push t
    else
      match t.get with
      | .error e => exceptions := exceptions.push e
      | .ok none => pure ()
      | .ok (some result) =>
        if let some idx ‚Üê findDuplicate result results then
          if result < results[idx]! then
            results := results.modify idx ({ ¬∑ with filtered := none })
            results := results.binInsert (¬∑ < ¬∑) result
          else
            results := results.binInsert (¬∑ < ¬∑) { result with filtered := none }
        else
          results := results.binInsert (¬∑ < ¬∑) result
  return (exceptions, { s with pending, results })
where
  /-- Check if there is already a duplicate of `result` in `results`,
  for which both appear in the filtered view. -/
  findDuplicate (result : ApplyResult) (results : Array ApplyResult) : MetaM (Option Nat) := do
    unless result.filtered.isSome do
      return none
    results.findIdxM? fun res =>
      pure res.filtered.isSome <&&> res.info.isDuplicate result.info

/-- Render one section of rewrite results. -/
def SectionState.render (filter : Bool) (s : SectionState) : Option Html := do
  let head ‚Üê s.results[0]?
  let suffix := match s.kind with
    | .hypothesis => " (local hypotheses)"
    | .fromFile => " (lemmas from current file)"
    | .fromImport => ""
  let suffix := if s.pending.isEmpty then suffix else suffix ++ " ‚è≥"
  let htmls := if filter then s.results.filterMap (¬∑.filtered) else s.results.map (¬∑.unfiltered)
  guard (!htmls.isEmpty)
  return mkListElement htmls
    <span> apply: <InteractiveCode fmt={head.pattern}/> {.text suffix} </span>

end InfoviewSearch.Apply
